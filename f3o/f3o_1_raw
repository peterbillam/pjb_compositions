5 systems 15 / 19 25 19 25 19 / 19 25 19 25 19 / 19 25 19 25 19 \
 / 19 25 19 25 19 / 19 25 19 25 19 /
pagenum 2
innerhead I, Flute Trio, Peter Billam
title Flute  Trio
leftfoot (c) 2011 Peter J Billam
midi channel 0 patch 73 pan 50
midi channel 1 patch 73 pan 05
midi channel 2 patch 73 pan 95

#midi off

% up or down,   stacc or leg,
% 5-note apregg at 10 note/sec  or  4-note arpegg ar 7.5 notes/sec
% even arpeggs  or  long top-note (quasi tune)
% perhaps a 4tet, with a sustained alto flute line underneath ?
% It Doesn't Have To Be A Canon
% Breath ?  Perhaps use it as a source of fractal patterns ?
% repeat a beat 3 times and it becomes a unison riff
% repeat a beat 3 times and it becomes a unison riff

/
4 bars | 9/16 36 | 36 | 36 | 36 |
| 9/16 1.2
=1' treble 9/16 cha0 leg95 16 E rest rest 8. rest rest
=2' treble 9/16 cha1 leg95 8. rest 16 E rest rest 8. rest
=3' treble 9/16 cha2 leg95 8. rest rest 16 E rest rest
|
=1' 16 [E F# E] [F# E F#] 8 [A 16 B]
=2' 8. rest 16 [E F# E] [F# E F#]
=3' 8. rest rest 16 [E F# E]
|
=1  16 [E F# E] [F# E F#] 8 [c 16 B]
=2' 8 [A 16 B] [E F# E] [F# E F#]
=3' 16 [F# E F#] 8 [A 16 B] [E F# E]
|
=1' 16 [E F# E] [F# E F#] 8 [A 16 B]
=2  8 [c 16 B] [E F# E] [F# E F#]
=3  16 [F# E F#] 8 [c 16 B] [E F# E]
|
il0.5 .03 5
=1  16 [E F# E] [F# E F#] 8 [c 16 B]
=2' 8 [A 16 B] [E F# E] [F# E F#]
=3' 16 [F# E F#] 8 [A 16 B] [E F# E]
|
=1  16 [E F# E] [A, c e] [F# rest, E]
=2  8 [c 16 B] [E F# E] [A, c e]
=3  16 [F# E F#] 8 [c 16 B] [E F# E]
|
=1  16 [A, c e] [E rest, F#] [A, c e]
=2  16 [F# rest, E] [A, c e] [E rest, F#]
=3  16 [A, c e] [F# rest, E] [A, c e]
| 1.6
=1, 12/16 16 [f# e c A] [f# e c A] [f# e c A]
=2, 12/16 16 [A, c e f#] [e c A f#] [e c A f#]
=3  12/16 16 [E rest, F# A] [c e f# e] [c A, f# e]
| 1.2
=1  9/16 16 [g eb c] [A G A] [G A G]
=2  9/16 16 [e c A,] [g eb c] [A G A]
=3, 9/16 16 [c A f#] [e c A] [g eb c]
|
il0.5 .03 10
=1  8 [d 16 eb] [A G A] [G A G]
=2  16 [G A G] 8 [d 16 eb] [A G A]
=3  16 [A G A] [G A G] 8 [d 16 eb]
|
=1  8 [c 16 B] [A rest G] [A G A]
=2  16 [G A G] 8 [c 16 B] [A rest G]
=3  16 [A G A] [G A G] 8 [c 16 B]
|
=1  16 [G, A, eb] a rest' rest' 8. rest
=2  16 [A G A] [G, A, eb] a rest' rest'
=3  16 [A rest G] [A G A] [G, A, eb]
|
=1' 16 [E F# E] [F# E F#] 8 [A 16 B]
=2' 8. rest 16 [E F# E] [F# E F#]
=3  16 a rest' rest' 8. rest 16 [E F# E]
|
=1  16 [E F# E] [F# E F#] 8 [c 16 B]
=2' 8 [A 16 B] [E F# E] [F# E F#]
=3' 16 [F# E F#] 8 [A 16 B] [E F# E]
|
il0.5 .03 15
=1' 16 [E F# E] [F# E F#] 8 [A 16 B]
=2  8 [c 16 B] [E F# E] [F# E F#]
=3  16 [F# E F#] 8 [c 16 B] [E F# E]
| 1.6
=1  12/16 16 [E F# E] [A, c e] [F# rest G] [A, c e]
=2  12/16 8 [A 16 B'] [E F# E] [A, c e] [F# rest G]
=3  12/16 16 [F# E F#] 8 [A 16 B'] [E F# E] [A, c e]
| 1.2
=1, 9/16 16 [f# e c] [A f# e] [c A f#]
=2, 9/16 16 [A, c e] [f# e c] [A f# e]
=3, 9/16 16 [F# rest G] [A, c e] [f# e c]
# [e c A]
| 1.2
=1  16 [g eb c] [A G A] 8 [eb 16 d]
=2  16 [c A, f#] [g eb c] [A G A]
=3, 16 [A f# e] [c A f#] [g eb c]
| 1.6
=1  12/16 16 [G A G] 8 [c 16 B] [A rest G] [A G A]
=2  12/16 8 [eb 16 d] [G A G] 8 [c 16 B] [A rest G]
=3  12/16 16 [A G A] 8 [eb 16 d] [G A G] 8 [c 16 B]
| 1.2
il0.5 .03 20
=1  9/16 16 [G, B, f] b rest rest 8. rest
=2  9/16 16 [A G A] [G, B, f] b rest rest
=3  9/16 16 [A rest G] [A G A] [G, B, f]

# midi off
#midi pause 2.0
/
3 bars | 48 | 48 | 48 |
| 1.6
# sounds better if the e's are leg190 :-(
#=1, leg99 16 [B e b] [e~ A e] [b e~ G] [e b e~]
# mt2 ? e~ b : b e~ ?
=1, leg99 16 [B e e~] [b A e] [b e~ G] [e b e~]
=2, leg99 8. rest 16 [B e e~] [b A e] [b e~ G]
=3, leg99 16 b rest rest 8. rest 16 [B e e~] [b A e]
|
#=1' 16 [A e b] [e~ B e] [b e~ A] [e b e~]
=1, 16 [A e e~] [b B e] [b e~ A] [e e~ b]
=2, 16 [e b e~] [A e e~] [b B e] [b e~ A]
=3, 16 [b e~ G] [e b e~] [A e e~] [b B e]
|
#=1' 16 [G e b] [e~ A e] [b e~ B] [e b e~]
=1, 16 [G e e~] [b A e] [b e~ B] [e b e~]
=2, 16 [e e~ b] [G e e~] [b A e] [b e~ B]
=3, 16 [b e~ A] [e e~ b] [G e e~] [b A e]
|
=1, 16 [A# e e~] [b G# e] [e~ b A#] [e b e~]
=2, 16 [e b e~] [A# e e~] [b G# e] [e~ b A#]
=3, 16 [b e~ B] [e b e~] [A# e e~] [b G# e]
|
il0.5 .03 25
=1, 16 [F# e e~] [b G# e] [b e~ A#] [e b e~]
=2, 16 [e b e~] [F# e e~] [b G# e] [b e~ A#]
=3, 16 [e~ b A#] [e b e~] [F# e e~] [b G# e]
|
=1, leg99 16 [B e e~] [b A e] [b e~ G] [e e~ b]
=2, leg99 16 [e b e~] [B e e~] [b A e] [b e~ G]
=3, leg99 16 [b e~ A#] [e b e~] [B e e~] [b An e]
|
=1, 16 [A e e~] [b B e] [b e~ Bb] [e d~ bb]
=2, 16 [e e~ b] [A e e~] [b B e] [b e~ Bb]
=3, 16 [b e~ G] [e e~ b] [A e e~] [b B e]
|
=1, 16 [A e bb] [d~ G e] [bb d~ A] [e d~ bb]
=2, 16 [e d~ bb] [A e bb] [d~ G e] [bb d~ A]
=3, 16 [b e~ Bb] [e d~ bb] [A e bb] [d~ G e]
|
=1, 16 [Bb e c~#] [bb A e] [bb c~# G] [e bb c~#]
=2, 16 [e d~ bb] [Bb e c~#] [bb A e] [bb c~# G]
=3, 16 [bb d~ A] [e d~ bb] [Bb e c~#] [bb A e]
|
il0.6 .33 30
#=1, 163 [bb c~# bb] [c~# c~n bb] 16 [a g] 163 [f# e eb]
=1, 163 [c~# c~n bb] 16 [a g] 163 [f# e eb] [db c Bb]
=2, 163 [e bb c~#] [c~# c~n bb] 16 [a g] 163 [f# e eb]
=3, 163 [bb c~# G] [e bb c~#] [c~# c~n bb] 16 [a g]
| 1.2
# =1, 163 [db c Bb] 16 [A G] 16 F# 16. rest 8 rest
=1  16 [A G] 163 F# rest rest 8 rest
=2  163 [db c Bb] 16 [A G] 163 F# rest rest
=3  163 [f# e eb] [db c Bb] 16 [A G]

# now 5x83 descending, top and bottom notes at 12 st and moving ?
# but it doesn't seem to sound shimmery in practice ...
# perhaps a staccato section ?

| 4/4 1.6
=1' 163 leg65 [F#-. A-. c#-.] [eb-ten leg65 F#-. A-.] [c#-. en-ten leg65 F#-.] [A-. c#-. e-ten]
=2' 8 rest 163 leg65 [F#-. A-. c#-.] [eb-ten leg65 F#-. A-.] [c#-. en-ten leg65 F#-.]
=3' 163 F# rest rest 8 rest 163 leg65 [F#-. A-. c#-.] [eb-ten leg65 F#-. A-.]
|
=1' 163 leg65 [F#-. A-. c#-.] [eb-ten leg65 F#-. A-.] [c#-. en-ten leg65 F#-.] [A-. c#-. eb-ten]
=2' 163 leg65 [A-. c#-. e-ten] [F#-. A-. c#-.] [eb-ten leg65 F#-. A-.] [c#-. en-ten leg65 F#-.]
=3' 163 leg65 [c#-. en-ten leg65 F#-.] [A-. c#-. e-ten] [F#-. A-. c#-.] [eb-ten leg65 F#-. A-.]
|
=1' 163 leg65 [F#-. A-. c#-.] [eb-ten leg65 F#-. A-.] [c#-. en-ten leg65 G-.] [A-. c#-. e-ten]
=2' 163 leg65 [A-. c#-. eb-ten] [F#-. A-. c#-.] [eb-ten leg65 F#-. A-.] [c#-. en-ten leg65 G-.]
=3' 163 leg65 [c#-. en-ten leg65 F#-.] [A-. c#-. eb-ten] [F#-. A-. c#-.] [eb-ten leg65 F#-. A-.]
|
il0.9 .03 35
=1' 163 leg65 [G-. A-. c#-.] [f-ten leg65 G-. A-.] [c#-. f-ten leg65 G-.] [A-. c#-. en-ten]
=2' 163 leg65 [A-. c#-. e-ten] [G-. A-. c#-.] [f-ten leg65 G-. A-.] [c#-. f-ten leg65 G-.]
=3' 163 leg65 [c#-. en-ten leg65 G-.] [A-. c#-. e-ten] [G-. A-. c#-.] [f-ten leg65 G-. A-.]
|
=1' 163 leg65 [G-. A-. c#-.] [f-ten leg65 G-. A-.] [c#-. en-ten leg65 G-.] [Bb-. c#-. e-ten]
=2' 163 leg65 [A-. c#-. en-ten] [G-. A-. c#-.] [f-ten leg65 G-. A-.] [c#-. en-ten leg65 G-.]
=3' 163 leg65 [c#-. f-ten leg65 G-.] [A-. c#-. en-ten] [G-. A-. c#-.] [f-ten leg65 G-. A-.]
|
=1' 163 leg65 [G-. Bb-. c#-.] [e-ten leg65 G-. Bn-.] [c#-. f-ten leg65 G-.] [Bn-. c#-. f#-ten]
=2' 163 leg65 [Bb-. c#-. e-ten] [G-. Bb-. c#-.] [e-ten leg65 G-. Bn-.] [c#-. f-ten leg65 G-.]
=3' 163 leg65 [c#-. en-ten leg65 G-.] [Bb-. c#-. e-ten] [G-. Bb-. c#-.] [e-ten leg65 G-. Bn-.]
| 1.2
=1 163 [g eb B,] G rest rest 8 rest
=2 163 [Bn-. c#-. f#-ten] [g eb B,] G rest rest
=3, 163 [c#-. f-ten leg65 G-.] [Bn-. c#-. f#-ten] [g eb B,]

# then brief recap of opening bars
| 1.2
=1  leg101 16 [G A G] [A G A] 8 [c 16 d]
=2  leg101 8. rest 16 [G A G] [A G A]
=3  leg101 16 G rest rest 8. rest 16 [G A G]
|
il0.6 .3 40
=1 16 [G A G] [A G A] 8 [eb 16 d]
=2 8 [c 16 d] [G A G] [A G A]
=3 16 [A G A] 8 [c 16 d] [G A G]
|
=1  16 [G A G] [A G A] 8 [c 16 d]
=2  8 [eb 16 d] [G A G] [A G A]
=3  16 [A G A] 8 [eb 16 d] [G A G]
|
=1 16 [G A G] [A G A] [eb f ab]
=2 8 [c 16 d] [G A G] [A G A]
=3 16 [A G A] 8 [c 16 d] [G A G]
|
=1 2 bn 16 rest''
=2 16 [eb f ab] 4. bn
=3 16 [A G A] [eb f ab] 8 b 16 rest
|
=1 8. b 16 [eb f eb 8. Ab,]
=2  4. b 16 [eb f eb]
=3  2 b 16 rest'
|
il0.6 .1 45
=1 16 [f eb f 8 b] 16 rest [d f d]
=2 8. Ab 16 [f eb f 8 b] 16 rest
=3, 16 [eb f eb 8. Ab] 16 [f eb f]
|
=1 8. Ab 16 [e d e 8. bb]
=2, 16 [d f d 8. Ab,] 16 [e d e]
=3 8 b 16 rest [d f d 8. Ab,]
|
=1 16 [d e d 8. Ab,] 16 [e d e]
=2, 8. bb 16 [d e d 8. Ab,]
=3 16 [e d e] 8. bb 16 [d e d]
|
=1 8 bb 16 rest [c# e c# 8. Bb,]
=2 16 [e d e 8 bb] 16 rest [c# e c#]
=3 8. Ab 16 [e d e 8 bb] 16 rest
|
=1 16 [d# c# d# 8. ab] 16 [c# d# c#]
=2 8. Bb 16 [d# c# d# 8. ab]
=3 16 [c# e c# 8. Bb,] 16 [d# c# d#]
|
il0.6 .1 50
=1 8. Bn 16 [e dn e 8 ab] 16 rest
=2 16 [c# d# c# 8. Bn,] 16 [e dn e]
=3  8. ab 16 [c# d# c# 8. Bn,]

|
=1 16 [e d e 8. Bb,] 16 [c# e c#]
=2 8 ab 16 rest [e d e 8. Bb,]
=3 16 [e dn e 8 ab] 16 rest [e d e]
|
=1 8. g 16 [e d e 8. A,]
=2 16 [c# e c# 8. g] 16 [e d e]
=3 8. Bb 16 [c# e c# 8. g]
|
=1 16 [c# d# c# 8 a] 16 rest [d# c# d#]
=2 8. A 16 [c# d# c# 8 a] 16 rest
=3 16 [e d e 8. A,] 16 [c# d# c#]
|
#=1 8. A 16 [c# d# c# 8 bn] 16 rest
=1 8. G 16 [c# d# c# 8 bn] 16 rest
=2 16 [d# c# d# 8. G,] 16 [c# d# c#]
=3 8 a 16 rest [d# c# d# 8. G,]
#|
#i0.5 .32 should go straight to eb f ?
#=1 16 [e d e] 8. Ab 16 [d e d]
#=2 8 bn 16 rest [e d e] 8. Ab
#=3 16 [c# d# c#] 8 bn 16 rest [e d e]
#|
#=1 8. b 16 [f d f] 8. Ab
#=2 16 [d e d] 8. b 16 [f d f]
#=3 8. Ab 16 [d e d] 8. b
|
il0.6 .1 55
=1 16 [eb f eb] 4. b
#=2 8. Ab 16 [eb f eb] 8. b
=2 8 b 16 rest [eb f eb] 8 b 16 rest''
#=3 16 [f d f] 8. Ab 16 [eb f eb]
=3 16 [f d f] 8 b 16 rest [eb f eb]

|
=1 8. b 16 [an f eb] [A G A]
=2 4. b 16 [an f eb]
=3 2 b 16 rest'
|
=1 16 [f eb f] [G A G] [eb f eb]
=2 16 [A G A] [f eb f] [G A G]
=3 16 [an f eb] [A G A] [f eb f]
|
=1 16 [db eb db] [B c# B] [A B' A]
=2 16 [eb f eb] [db eb db] [B c# B]
=3 16 [G A G] [eb f eb] [db eb db]
|
=1 8 [G 16 A] 8. G 8. rest
=2 16 [A B' A] 8 [G 16 A] 8. G
=3 16 [B c# B] [A B' A] 8 [G 16 A]



#/
#|
#=1 1 blank
#/

# midi on
# midi pause 1.0

$B2 = {
	| 1.067
	=2, 16 [$N4 $N1 $N2 $N3] [$N4 $N1 $N2 $N3]
	=3, 16 [$O1 $O2 $O3 $O4] [$N1 $N2 $N3 $N4]
	$O1 == $N1
	$O2 == $N2
	$O3 == $N3
	$O4 == $N4
}

#solo1
$N1 = G
$N2 = A
$N3 = B
$N4 = d
| 8/16 1.1
=1, 8/16 leg100 vol110 4 rest 16 rest [c# d e]
=2' 8/16 leg101 vol85 16 rest [$N1 $N2 $N3] [$N4 $N1 $N2 $N3]
=3' 8/16 leg101 vol85 8. G 16 rest 16 [$N1 $N2 $N3 $N4]

$O1 == $N1
$O2 == $N2
$O3 == $N3
$O4 == $N4

$B2
=1, 16 [d c# 8 B] 4 rest

$N3 = c
$N4 = eb
$B2
=1, 8 rest 16 [eb f] [g f eb d]
$N3 = c#
$N4 = e
$B2
=1, 16 [c# d# e] rest 4 rest

$B2
=1, 8 rest 16 [e f#] [g f# e d]
$N3 = c
$N4 = eb
$B2
=1, 16 [c B 8 A] 4 rest

$N3 = B
$N4 = d
$B2
=1,  leg100 4 rest 16 rest [c# d e]
$B2
=1, 16 [d c# 8 B] 4 rest

$N3 = c
$N4 = eb
$B2
=1, 8 rest 16 [f g] [a g f eb]
$N3 = c#
$N4 = e
$B2
=1, 16 [d e 8 f#] 16 rest [e f# g]

$N3 = d
$N4 = f
$B2
=1, 16 [a g fn] rest rest [e f g]
$B2
=1, 16 [a g 8 a] 4 rest

$N3 = c#
$N4 = e
$B2
=1, 8 rest 16 [g a] [b a g f#]
$N3 = c
$N4 = eb
$B2
=1, 16 [g fn 8 e] 8 rest 16 [g a]

$N2 = Bb
$B2
=1, 16 [bb a g f#] [g fn 8 eb]
$N3 = c#
$N4 = e
$B2
=1, 16 rest [g a bb] [a g f] rest
=1,
$N3 = d
$N4 = f
$B2
=1, 16 rest [g f e] [f e 8 f]
$B2
=1, 8 rest 16 [g a] 8 [bb 16 g f]

$N3 = c#
$N4 = e
$B2
=1, 16 [e f 8 g] 4 rest
$N3 = c
$N4 = eb
$B2
=1, 16 rest [f g a] [g f 8 eb]
$B2
=1, 8 rest 16 [a bb] 8 [c~ 16 bb a]

$N3 = c#
$N4 = e
$B2
=1, 16 [g f# 8 g] rest 16 [a g]
$N3 = d
$N4 = f
$B2
=1, 16 [fn e f g] [f e 8 d(1]
$B2
=1, 8 d)1 rest 16 rest [f e d]
$N3 = c#
$N4 = e
$B2
=1, 16 [e f 8 g(1] 4 g)1(1

$N2 = B
$B2
=1, 4 g)1 8 rest 16 [g a]

$N3 = d
$N4 = f
$B2
=1, 8 [g 16 g~{1 g}1] 8 rest G
$B2
=1, 16 [g{1 G}1] 8 rest [g 16 g~{1 g}1]
$N3 = c#
$N4 = e
$B2
=1, 8 rest G 16 [g{1 G}1] 8 rest 
$N3 = d
$N4 = f
$B2
=1, 8 [g 16 g~{1 g}1] 8 rest G 

$N2 = c
$B2
=1, 16 [g{1 G}1] 8 rest 8 [g 16 g~{1 g}1]
$B2
=1, 8 rest G 16 [g{1 G}1] 8 rest 
$B2
=1, 8 [a 16 g a] 8 [bb 16 b~b{1 bb}1]
$N1 = d
$B2
=1, 8 rest Bb 16 [bb{1 Bb}1] 8 rest
$N1 = Bb
$B2
=1, 8 [Bb 16 bb{1 Bb}1] 8 [bb 16 Bb bb(1]

# Now: descending cascade a# g# e c f# d bb
# in 1 then 2 then 3 flutes
# Then as bar 60... but +3st, and with flute2 taking the solo
#
# Then later starting on db with flute3 taking the solo,
# possibly a fibonacci ? [85 85 85 85 85] : [43 43 43] : 4 4 ?
|
il0.7 .25 95
#=1 2 bb
# or longer and do a proper cresc,dim on it
#=2 4 rest leg70 16 [a# g# a# g#]
#=3 4 rest leg70 8 rest 16 [g# a#]
#| 1.03
=1 4. bb)1 8 rest
=2 leg85 16 [a# g# a# g#] rest [g# a# g#]
=3 leg85 8 rest 16 [g# a#] [g# a# g# a#]
|
=1 2 rest
=2 16 [a# g# a# g#] [a# g# a# g#]
=3 16 [g# a# g# a#] rest [a# g# a#]
|
=1 2 rest
=2 16 [a# g# a# g#] rest [g# a# g#]
=3 16 [g# a# g# a#] [g# a# g#] rest
|
=1 leg90 16 [a#{1 g# e a#}1 g#{1 e}1] 8 rest
=2 leg90 8 rest 16 [a#{1 g#}1] [e{'1 a# g# e}'1]
=3 leg90 4 rest 16 [a#{1 g# e a#}1]
|
=1 16 rest [a#{1 g# e}1] [a#{1 g# e}1] rest
=2 8. rest 16 a# [g#{1 e a# g#}1]
=3 16 [g#{1 e}1] 8 rest 16 rest [a#{1 g# e}1]
|
il0.7 .02 100
=1 8 rest 16 [a#{1 g#}1] [e{'1 a# g# e}'1]
=2 16 e 8. rest 16  [a#{1 g# e a#}1]
=3 16 [a#{1 g# e}1] 16 rest 8 rest 16 [a#{1 g#}1]
|
=1  leg95  8. rest 16 a# [g#{1 e c a#}1]
=2  leg95 16 [g#{1 e}1] 8 rest 16 rest [g#{1 e c}1]
=3  leg95 16 [e{'1 a# g# e}'1] 8. rest 16 e
|
=1 leg99 16 [g#{1 e}1] 8 rest 16 [a#{1 g# e c}1]
=2 leg99 16 [a#{1 g# e c}1] 8 rest 16 [a#{1 g#}1]
=3 leg99 16 [c{''1 a# g# e}1] c 4 rest
|
=1 8 [f# 16 c{1 e}1] [g#{1 a#}1 f#] rest
=2 16 [e{1 c}1 8 f#] 16 [c{1 e g# a#}1]
=3 16 [a#{1 g# e c}1] 8 [f# 16 c{1 e}1]
|
=1 16 [a#{1 g# e c}1] 8 [f# 16 a#{1 g#}1]
=2 16 [f# rest' a#{1 g#}1] [e{1 c}1 8 f#]
=3 16 [g#{1 a#}1 f#] 16 rest [a#{1 g# e c}1]
|
il0.7 .02 105
=1 16 [e{1 c}1 8 f#] [d 16 a#{1 g#}1]
=2 16 [a#{1 g# e c}1] 8 [f# d]
=3  8 [f# 16 a#{1 g#}1] [e{1 c}1 8 f#]
|
=1 16 [e{1 c}1 8 f#] [d 16 a#{1 g#}1]
=2 16 [a#{1 g# e c}1] 8 [f# d]
=3  8 [d 16 a#{1 g#}1] [e{1 c}1 8 a#]
|
#=1 16 [e{1 c}1 8 f#] [d 16 a#{1 g#}1]
#=2 16 [a#{1 g# e c}1] 8 [f# d]
#=3  8 [d 16 a#{1 g#}1] [e{1 c}1 8 a#]
#|
=1 16 [e{1 c}1 f# d] Bb rest rest a#
=2 16 [a#{1 g# e c}1] [f# d Bb] rest
=3  8 [d 16 a#{1 g#}1] [e{1 c}1 f# d]
|
=1 16 [g#{1 e c f#}1] [d{1 Bb}1] 8 rest
=2 16 rest [a#{1 g# e}1] [c{'1 f# d Bb}1]
=3 16 Bb 8 rest 16 a# [g#{1 e c f#}1]
|
#=1  16 [g#{1 e c f#}1] [d{1 Bb}1] 8 rest
#=2  8 rest 16 [g#{1 e}1] [c{'1 f# d Bb}1]
#=3  16 [d{1 Bb}1] 8 rest 16 [g#{1 e c f#}1]
#|
=1 16 rest [g#{1 e c}1] [f#{1 d Bb}1] rest
=2 8. rest 16 g# [e{1 c f# d}1]
=3 16 [d{1 Bb}1] 8 rest 16 rest [g#{1 e c}1]
|
il0.9 .01 110
=1 16 rest [e{1 c f#}1] [d{1 Bb}1] 8 rest
=2 16 Bb rest rest e [c{'1 f# e d}1]
=3 16 [f#{1 d Bb}1] rest rest [e{1 c f#}1]
|
=1 16 [e{1 c d Bb}1] rest [c{1 d Bb}1]
=2 16 rest [e{1 c d}1] Bb rest [c{1 d}1]
=3 16 [d{1 Bb e c}1] [d{1 Bb}1] rest c
#|
#=1 16 [c{1 Bb}1] rest [e{1 c d}1]
#=2 16 rest [e{1 c d}1] Bb rest [e{1 c}1]
#=3 16 [d{1 Bb e c}1] [d{1 Bb}1] rest e
#|
#=1 16 [Bb rest c{1 d}1] [Bb{1 c Bb}1] rest
#=2 16 [d{1 Bb}1 rest c] [d{1 Bb c Bb}1]
#=3 16 [c{1 d Bb}1] rest [c{1 d Bb c}1]
|
=1  8 rest 16 [c{1 d}1] Bb rest rest 16 c
=2  16 Bb 8. rest 4 rest
=3 16 [d{1 Bb}1] 8 rest 16 [c{1 d Bb}1] rest

il1.1 .01 115
$B2 = {
	| 1.067
	=3, 16 [$N4 $N1 $N2 $N3] [$N4 $N1 $N2 $N3]
	=1, 16 [$N1 $N2 $N3 $N4] [$N1 $N2 $N3 $N4]
	$O1 == $N1
	$O2 == $N2
	$O3 == $N3
	$O4 == $N4
}
$N1 = Bb
$N2 = c
$N3 = d
$N4 = f
$BS = morse_thue? 163 [$NS $NS-I3 $NS] : 16 [$NS $NS] : 8 $NS ?
$NS = cycle? $S1 : $S2 : ?
| 1.1
il0.4 .05 113
=1, leg100 vol85 16 Bb 8. rest leg55 16 [$N1 $N2 $N3 $N4]
i0.9 .20 non legato
=2, leg101 vol110 leg101 4 rest
i0.1 .02 non legato
=3, leg101 vol85 16 leg101 rest [$N1 $N2 $N3] [$N4 $N1 $N2 $N3]

$O1 == $N1
$O2 == $N2
$O3 == $N3
$O4 == $N4
$S1 = g
$S2 = f
$B2
=2 8 rest $BS $BS $BS
i0.9 .15 legato
# =2 8 rest ab 16 [g 8 ab 16 g]

$N3 = eb
$N4 = gb
$S1 = gb
$S2 = ab
$B2
=2 $BS $BS $BS $BS
# =2 8 [ab 16 bb c~(1] 4 c~)1

$N3 = en
$N4 = gn
$S1 = d~
$S2 = c~
$B2
=2 $BS $BS 8 rest $BS

$N3 = eb
$N4 = gb
$S1 = ab
$S2 = bb
$B2
=2 $BS $BS $BS $BS

$N3 = d
$N4 = f
$S1 = c~
$S2 = bb
$B2
=2 $BS $BS $BS $BS

$N3 = eb
$N4 = gb
$S1 = bb
$S2 = ab
$B2
=2 8 rest $BS $BS $BS

$N3 = en
$N4 = gn
$S1 = bb
$S2 = c~
$B2
=2 $BS $BS $BS $BS

$N3 = f
$N4 = ab
$S1 = d~
$S2 = e~b
$B2
=2 $BS $BS 8 rest rest

$N3 = en
$N4 = gn
$S1 = c~
$S2 = d~
$B2
=2 $BS $BS $BS $BS

$N3 = eb
$N4 = gb
$S1 = ab
$S2 = bb
$B2
=2 $BS $BS $BS $BS

$N2 = db
$S1 = c~
$S2 = bb
$B2
=2 $BS 8 rest $BS $BS

$N3 = en
$N4 = gn
$S1 = a
$S2 = g
$B2
=2 $BS $BS $BS $BS

$N3 = f
$N4 = ab
$S1 = e~b
$S2 = d~b
$B2
=2 $BS $BS $BS 8 rest

$N3 = en
$N4 = gn
$S1 = c~
$S2 = bb
$B2
=2 $BS $BS $BS $BS

$N2 = dn
$B2
=2 $BS $BS $BS $BS

$N3 = f
$N4 = ab
$B2
=2 $BS 8 rest rest $BS

$N2 = eb
$S1 = c~
$S2 = d~b
$B2
=2 $BS $BS $BS $BS

$S1 = e~b
$S2 = d~b
$B2
# bar 129
=2 $BS $BS $BS $BS

$N1 = Bn
$B2
=2 8 rest $BS $BS $BS

$B2
$S2 = f~
=2 $BS $BS $BS $BS

$N1 = c
$B2
#$S1 = g~
=2 $BS $BS 8 rest $BS

$N1 = db
$S1 = g~b
$S2 = a~b
$B2
=2 $BS $BS $BS $BS

$B2
# a~b to b~b is not a clean-functioning trill-fingering
# $S1 = b~b
=2 $BS $BS $BS $BS


| 1.2
=1 8. rest g 16 [fn e d]
=2 8. g~ 16 [f~n e~ d~] 82 [c~# b]
=3 8. rest rest g
|
=1 82 [c# B] 16 [Bb' Ab G] [f e d]
=2 16 [bb ab g] [f e d] 82 [c# B]
=3 16 [fn e d] 82 [c# B] 16 [Bb' Ab G]
|
=1 82 [c# B] 16 [Bb' A Ab] [G Gb F]
=2 16 [Bb' A Ab] [G Gb F] 82 [e d]
=3 16 [f e d] 82 [c# B] 16 [Bb' A Ab]
|
il0.5 .02 140
=1 82 [E D] 16 [db c B] [Bb' A Ab]
=2 16 [db c B] [Bb' A Ab] 82 [G F]
=3 16 [G Gb F] 82 [E D] 16 [db c B]

|
=1 82 [G F] 16 E 8 rest 8. rest
=2 16 E 8 rest 8. rest rest
=3 16 [Bb' A Ab] 82 [G F] 16 E 8 rest

|
i1.1 .01 page 11, recap :-)
=1' leg95 16 [E F# E] [F# E F#] 8 [A 16 B]
=2' leg95 8. rest 16 [E F# E] [F# E F#]
=3' leg95 8. rest rest 16 [E F# E]
|
=1  16 [E F# E] [F# E F#] 8 [c 16 B]
=2' 8 [A 16 B] [E F# E] [F# E F#]
=3' 16 [F# E F#] 8 [A 16 B] [E F# E]
|
=1' 16 [E F# E] [F# E F#] 8 [A 16 B]
=2  8 [c 16 B] [E F# E] [F# E F#]
=3  16 [F# E F#] 8 [c 16 B] [E F# E]
| 1.6
il0.4 .04 145
=1  12/16 16 [E F# E] [A, c e] [F# rest G] [A, c e]
=2  12/16 8 [A 16 B'] [E F# E] [A, c e] [F# rest G]
=3  12/16 16 [F# E F#] 8 [A 16 B'] [E F# E] [A, c e]
| 1.2
=1, 9/16 16 [f# e c] [A f# e] [c A f#]
=2, 9/16 16 [A, c e] [f# e c] [A f# e]
=3, 9/16 16 [F# rest G] [A, c e] [f# e c]
|
=1 16 [g eb c] [A G A] [G, B f]
=2, 16 [c A f#] [g eb c] [A G A]
=3, 16 [A f# e] [c A f#] [g eb c]
| 0.8
=1 16 b rest rest 8. rest
=2 16 [G, B f] b rest rest
=3 16 [A G A] [G, B f]

| 1.6
=1, leg99 16 [B e e~] [b A e] [b e~ G] [e b e~]
=2, leg99 8. rest 16 [B e e~] [b A e] [b e~ G]
=3, leg99 16 b rest rest 8. rest 16 [B e e~] [b A e]
|
il0.7 .01 150
=1, 16 [A e e~] [b B e] [b e~ A#] [e e~ b]
=2, 16 [e b e~] [A e e~] [b B e] [b e~ A#]
=3, 16 [b e~ G] [e b e~] [A e e~] [b B e]
|
=1, 16 [B e e~] [b G# e] [b e~ A#] [e b e~]
=2, 16 [e e~ b] [B e e~] [b G# e] [b e~ A#]
=3, 16 [b e~ A#] [e e~ b] [B e e~] [b G# e]
|
=1, 16 [F# e e~] [b G# e] [e~ b A#] [e b d~]
=2, 16 [e b e~] [F# e e~] [b G# e] [e~ b A#]
=3, 16 [b e~ A#] [e b e~] [F# e e~] [b G# e]
|
=1, 16 [B e d~] [b An e] [b c~# G] [e b c~#(1]
=2, 16 [e b d~] [B e d~] [b An e] [b c~# G]
=3, 16 [e~ b A#] [e b d~] [B e d~] [b An e]
|
#=1, 163 [bb c~# bb] [c~# c~n bb] 16 [a g] 163 [f# e eb]
=1, 163 [c~#)1 c~n bb] 16 [a g] 163 [f# e eb] [db c Bb]
=2, 163 [e bb c~#(1] [c~#)1 c~n bb] 16 [a g] 163 [f# e eb]
=3, 163 [bb c~# G] [e bb c~#(1] [c~#)1 c~n bb] 16 [a g]
| 1.6
# =1, 163 [db c Bb] 16 [A G] 16 F# 16. rest 8 rest
il0.4 155
=1  16 [A G] 163 F# rest rest 83. rest  83. rest
=2  163 [db c Bb] 16 [A G] 163 F# rest rest 83. rest
=3  163 leg100 [f# e eb] [db c Bb] 16 [A G] 163 F# 83 rest

# continuation with step-up to 10 notes/sec, from to_be_echoed
#| 0.4
#i0.3 .03 shortened this one beat and moved back into the previous bar
#=1 8. rest rest
#=2 8. rest
# 16 F# 8 rest
#=3 16 F# 8 rest

#| 1.2
#=1' 4 rest rest 16 [Gb Bb Ab Gb]
#=2' 4 rest 16 [Gb Bb Ab Gb] 8 [c 16 Gb Bb]
#=3' 16 [Gb Bb Ab Gb] 8 [c 16 Gb Bb] [Ab Gb 8 c#]



| 1.2
Il0.9 .01 new quarter-note = previous dotted eigth-note
=1' treble cha0  4 rest 16 F# 8. rest 16 [F# Bb G# F#]
=2' treble cha1  16 F# 8. rest 16 [F# Bb G# F#] 8 [c 16 F# Bb]
=3' treble cha2  16 [F# Bb G# F#] 8 [c 16 F# Bb] [G# F# 8 c#]
|
=1' 8 [c 16 F# Bb] 16 [G# F# 8 c#] 16 [F# Bb G# F#]
=2' 16 [G# F# 8 c#] 16 [F# Bb G# F#] 8 [d 16 F# Bb]
=3' 16 [F# Bb G# F#] 8 [d 16 F# Bb] [G# F# 8 eb]
|
=1' 8 [d 16 F# Bb] 16 [G# F# 8 eb] 16 [F# Bb G# F#]
=2' 16 [G# F# 8 eb] 16 [F# Bb G# F#] 8 [en 16 F# Bb]
=3' 16 [F# Bb G# F#] 8 [e 16 F# Bb] [G# F# 8 eb]

|
=1' 8 [e 16 F# Bb] 16 [G# F# 8 eb] 16 [F# Bb G# F#]
=2' 16 [G# F# 8 eb] 16 [F# Bb G# F#] 8 [d 16 F# Bb]
=3' 16 [F# Bb G# F#] 8 [d 16 F# Bb] [G# F# 8 c#]
|
il0.8 .24 160
=1' 8 [d 16 F# Bb] 16 [G# F# 8 c#] 16 [F# Bn G# F#]
=2' 16 [G# F# 8 c#] 16 [F# B G# F#] 8 [c# 16 F# B]
=3' 16 [F# B G# F#] 8 [c# 16 F# B] [G# F# 8 d]
|
=1' 8 [c# 16 F# B] 16 [G# F# 8 d] 16 [F# B G# F#]
=2' 16 [G# F# 8 d] 16 [F# B G# F#] 8 [eb 16 F# B]
=3' 16 [F# B G# F#] 8 [eb 16 F# B] [G# F# 8 en]

|
=1' 8 [eb 16 F# B] 16 [G# F# 8 en] 16 [F# B G# F#]
=2' 16 [G# F# 8 e] 16 [F# B G# F#] 8 [eb 16 F# B]
=3' 16 [F# B G# F#] 8 [eb 16 F# B] [G# F# 8 d]
|
=1' 8 [eb 16 F# B] 16 [G# F# 8 d] 16 [F# B A F#]
=2' 16 [G# F# 8 d] 16 [F# B A F#] 8 [c# 16 F# B]
=3' 16 [F# B A F#] 8 [c# 16 F# B] [A F# 8 d]
|
=1' 8 [c# 16 F# B] 16 [A F# 8 d] 16 [F# B A F#]
=2' 16 [A F# 8 d] 16 [F# B A F#] 8 [eb 16 F# B]
=3' 16 [F# B A F#] 8 [eb 16 F# B] [A F# 8 en]

|
il0.8 .28 165
=1' 8 [eb 16 F# B] 16 [A F# 8 en] 16 [F# B A F#]
=2' 16 [A F# 8 e] 16 [F# B A F#] 8 [eb 16 F# B]
=3' 16 [F# B A F#] 8 [eb 16 F# B] [A F# 8 d]
|
=1' 8 [eb 16 F# B] 16 [A F# 8 d] 16 [F# c G# F#]
=2' 16 [A F# 8 d] 16 [F# c G# F#] 8 [eb 16 F# c]
=3' 16 [F# c G# F#] 8 [eb 16 F# c] [G# F# 8 en]
|
=1' 8 [eb 16 F# c] 16 [G# F# 8 en] 16 [F# c G# F#]
=2' 16 [G# F# 8 e] 16 [F# c G# F#] 8 [eb 16 F# c]
=3' 16 [F# c G# F#] 8 [eb 16 F# c] [G# F# 8 d]

|
=1' 8 [eb 16 F# c] 16 [G# F# 8 d] 4 blank
=2' 16 [G# F# 8 d] 4 blank blank
=3'

=1 2 blank 16 [F# A c' Gn]
=2 4 blank 16 [F# A c' Gn] [Bb' c#' G# Bn']
=3 16 [F# A c' G] [Bb' c#' G# Bn'] [d A, cn eb]
|
il0.8 .04 170
=1 16 [Bb' c#' G# Bn'] [d A, cn eb] [A#, c# en B]
=2 16 [d A, cn eb] [A#, c# en B] [d f cn d#]
=3 16 [A#, c# e B] [d f cn d#] [f# c# e g]
|
=1 16 [d f c eb] [f# c# en g] [d fn g# eb]
=2 16 [f# c# e g] [d fn g# eb] [f# a en gn]
=3 16 [d f g# eb] [f# a en gn] [bb fn g# bn]
|
=1 16 [f# a e g] [bb fn g# bn] [f# a c~ gn]
=2 16 [bb f g# bn] [f# a c~ gn] [bb c~# g# bn]
=3 16 [f# a c~ g] [bb c~# g# bn] 16 d~ 8. rest
# [d~ a c~n e~b]

|
=1, 16 [bb c~# g# bn] d~ 8. rest 16 [f# bb g# f#]
=2, 16 d~ 8. rest 16 [f# bb g# f#] 8 [c~ 16 f# bb]
=3, 16 [f# bb g# f#] 8 [c~ 16 f# bb] [g# f# 8 c~#]

#|
#=1 16 [f# a e g] [bb fn g# bn] [f# a c~ gn]
#=2 16 [bb f g# bn] [f# a c~ gn] [bb c~# g# bn]
#=3 16 [f# a c~ g] [bb c~# g# bn] [f# bb g# f#]
#
#|
#i1.3 .01 moved this back
#=1, 16 [bb c~#-IXX g# bn] 16 [f# bb g# f#]
#=2, 16 [f# bb g# f#] 8 [c~ 16 f# bb]
#=3, 8 [c~ 16 f# bb] [g# f# 8 c~#]

|
=1, 8 [c~ 16 f# bb] 16 [g# f# 8 c~#] 16 [f# bb g# f#]
=2, 16 [g# f# 8 c~#] 16 [f# bb g# f#] 8 [d~ 16 f# bb]
=3, 16 [f# bb g# f#] 8 [d~ 16 f# bb] [g# f# 8 e~b]

|
il0.8 .09 175
=1, 8 [d~ 16 f# bb] 16 [g# f# 8 e~b] 16 [f# bb g# f#]
=2, 16 [g# f# 8 e~b] 16 [f# bb g# f#] 8 [e~n 16 f# bb]
=3, 16 [f# bb g# f#] 8 [e~ 16 f# bb] [g# f# 8 e~b]
|
=1, 8 [e~n 16 f# bb] 16 [g# f# 8 e~b] 16 [f# bb g# f#]
=2, 16 [g# f# 8 e~b] 16 [f# bb g# f#] 8 [d~ 16 f# bb]
=3, 16 [f# bb g# f#] 8 [d~ 16 f# bb] [g# f# 8 c~#]

|
=1 8 [d~ 16 f# bb-IXX] [g# f#--IXX 8 c~#] 16 [c~n a f# b]
=2 16 [g# f# 8 c~#] 16 [c~n a f# b] [ab fn bb g]
=3 16 [c~n a f# b] [ab fn bb g] [e a f# d#]
|
=1 16 [ab fn bb g] [e a f# d#] [g# fn dn gn]
=2 16 [e a f# d#] [g# fn dn gn] [e c# f# eb]
=3 16 [g# f d gn] [e c# f# eb] [cn fn d B]
/
3 bars | 48 | 48 | 5 4 4 4 3 2 2 2 2 2 2 2 ||
|
=1 16 [e c# f# eb] [cn fn d B] [en c# Bb eb]
=2 16 [c f d B] [e c# Bb eb] [cn A, d Bn]
=3 16 [e c# Bb eb] [cn A, d Bn] [G# c' Bb' Gn]
|
il0.9 .03 180
=1' 16 [c A d B] [G# c# Bb Gn] [cn A F# Bn]
=2' 16 [G# c# Bb Gn] [cn A F# Bn] [G# Fn Bb Gn]
=3' 16 [c A F# B] [G# Fn Bb Gn] [F# rest f#] rest
|
=1 16 f# 8. rest 4 rest rest
=2 16 F# 8. rest 4 rest rest
=3 16 f~# 8. rest 4 rest rest

midi pause 3.0

